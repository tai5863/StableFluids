<!DOCTYPE html>
<html lang="ja">
<head>
    <meta charset="UTF-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Stable Fluids</title>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/stats.js/r16/Stats.min.js"></script>
    <script src="main.js"></script>
    <script type="x-shader/x-vertex" id="vs">#version 300 es
        const vec3[4] POSITIONS = vec3[](
            vec3(-1.0, -1.0, 0.0),
            vec3(1.0, -1.0, 0.0),
            vec3(-1.0, 1.0, 0.0),
            vec3(1.0, 1.0, 0.0)
        );
        
        const int[6] INDICES = int[](
            0, 1, 2,
            3, 2, 1
        );

        void main(void) {
            vec3 position = POSITIONS[INDICES[gl_VertexID]];
            gl_Position = vec4(position, 1.0);
        }
    </script>
    <script type="x-shader/x-fragment" id="initialize_velocity_fs">#version 300 es
        precision highp float;

        out vec2 o_velocity;

        void main(void) {
            o_velocity = vec2(0.0);
        }
    </script>
    <script type="x-shader/x-fragment" id="initialize_density_fs">#version 300 es
        precision highp float;

        uniform vec2 u_resolution;

        out vec3 o_density;
        
        void main(void) {
            vec2 st = (2.0 * gl_FragCoord.xy - u_resolution) / min(u_resolution.x, u_resolution.y);
            vec3 density = vec3(0.0);

            st = step(0.5, fract(st));

            density = vec3(st.x);

            o_density = density;
        }
    </script>
    <script type="x-shader/x-fragment" id="add_density_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_density_texture;

        out vec3 o_density;

        void main(void) {
            vec3 density = texelFetch(u_density_texture, ivec2(gl_FragCoord.xy), 0).xyz;
            o_density = density;
        }
    </script>
    <script type="x-shader/x-fragment" id="diffuse_density_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_density_texture;
        uniform float u_grid_space;
        uniform float u_dt;
        uniform float u_diffuse;

        out vec3 o_density;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);

            float N = 1.0 / u_grid_space;

            float a = u_dt * u_diffuse * N * N;

            vec3 center = texelFetch(u_density_texture, coord, 0).xyz;
            vec3 left = texelFetch(u_density_texture, coord + ivec2(-1, 0), 0).xyz;
            vec3 right = texelFetch(u_density_texture, coord + ivec2(1, 0), 0).xyz;
            vec3 down = texelFetch(u_density_texture, coord + ivec2(0, -1), 0).xyz;
            vec3 up = texelFetch(u_density_texture, coord + ivec2(0, 1), 0).xyz;

            o_density = (center + a * (left + right + down + up)) / (1.0 + 4.0 * a);
        }
    </script>
    <script type="x-shader/x-fragment" id="advect_density_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_density_texture;
        uniform sampler2D u_velocity_texture;
        uniform float u_grid_space;
        uniform float u_dt;

        out vec3 o_density;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);

            vec2 velocity = texelFetch(u_velocity_texture, coord, 0).xy;
            vec2 prevPos = vec2(coord) * u_grid_space - u_dt * velocity;
            vec2 prevCoord = prevPos / u_grid_space;

            ivec2 i = ivec2(prevCoord);
            vec2 f = fract(prevCoord);

            vec3 density00 = texelFetch(u_density_texture, i, 0).xyz;
            vec3 density10 = texelFetch(u_density_texture, i + ivec2(1, 0), 0).xyz;
            vec3 density01 = texelFetch(u_density_texture, i + ivec2(0, 1), 0).xyz;
            vec3 density11 = texelFetch(u_density_texture, i + ivec2(1, 1), 0).xyz;

            o_density = mix(mix(density00, density10, f.x), mix(density01, density11, f.x), f.y);
        }
    </script>
    <script type="x-shader/x-fragment" id="add_external_force_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_velocity_texture;
        uniform float u_grid_space;
        uniform float u_dt;
        uniform float u_force_rad;
        uniform vec2 u_force_center;
        uniform vec2 u_force_direction;
        uniform float u_force_intensity;

        out vec2 o_velocity;

        void main(void) {
            vec2 velocity = texelFetch(u_velocity_texture, ivec2(gl_FragCoord.xy), 0).xy;
            vec2 ex_force = smoothstep(u_force_rad, 0.0, length(u_force_center - gl_FragCoord.xy) * u_grid_space) * normalize(u_force_direction) * u_force_intensity;
            o_velocity = velocity + u_dt * ex_force;
        }
    </script>
    <script type="x-shader/x-fragment" id="diffuse_velocity_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_velocity_texture;
        uniform float u_grid_space;
        uniform float u_dt;
        uniform float u_diffuse;
        uniform vec2 u_resolution;

        out vec2 o_velocity;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);
            ivec2 coordMax = ivec2(u_resolution);

            float N = 1.0 / u_grid_space;

            float a = u_dt * u_diffuse * N * N;

            ivec2 vL = coord + ivec2(-1, 0);
            ivec2 vR = coord + ivec2(1, 0);
            ivec2 vD = coord + ivec2(0, -1);
            ivec2 vU = coord + ivec2(0, 1);

            vec2 center = texelFetch(u_velocity_texture, coord, 0).xy;
            
            vec2 left = texelFetch(u_velocity_texture, vL, 0).xy;
            vec2 right = texelFetch(u_velocity_texture, vR, 0).xy;
            vec2 down = texelFetch(u_velocity_texture, vD, 0).xy;
            vec2 up = texelFetch(u_velocity_texture, vU, 0).xy;

            if (vL.x < 0) {
              left.x = -center.x;
            }

            if (vR.x > coordMax.x) {
              right.x = -center.x;
            }

            if (vD.y < 0) {
              down.y = -center.y;
            }

            if (vU.y > coordMax.y) {
              up.y = -center.y;
            }

            o_velocity = (center + a * (left + right + down + up)) / (1.0 + 4.0 * a);
        }
    </script>
    <script type="x-shader/x-fragment" id="advect_velocity_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_velocity_texture;
        uniform float u_grid_space;
        uniform float u_dt;
        uniform vec2 u_resolution;

        out vec2 o_velocity;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);
            ivec2 coordMax = ivec2(u_resolution);

            vec2 velocity = texelFetch(u_velocity_texture, coord, 0).xy;
            vec2 prevPos = vec2(coord) * u_grid_space - u_dt * velocity;
            vec2 prevCoord = prevPos / u_grid_space;

            ivec2 i = ivec2(prevCoord);
            vec2 f = fract(prevCoord);

            ivec2 vR = i + ivec2(1, 0);
            ivec2 vU = i + ivec2(0, 1);
            ivec2 vRU = i + ivec2(1, 1);

            vec2 velocity00 = texelFetch(u_velocity_texture, i, 0).xy;
            vec2 velocity10 = texelFetch(u_velocity_texture, vR, 0).xy;
            vec2 velocity01 = texelFetch(u_velocity_texture, vU, 0).xy;
            vec2 velocity11 = texelFetch(u_velocity_texture, vRU, 0).xy;

            if (vR.x > coordMax.x) {
              velocity10.x = -velocity00.x;
            } 

            if (vU.y > coordMax.y) {
              velocity01.y = -velocity00.y;
            }

            if (vRU.x > coordMax.x && vRU.y > coordMax.y) {
              velocity11 = -velocity00;
            }

            o_velocity = mix(mix(velocity00, velocity10, f.x), mix(velocity01, velocity11, f.x), f.y);
        }
    </script>
    <script type="x-shader/x-fragment" id="projection_step01_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_velocity_texture;
        uniform float u_grid_space;

        out vec2 o_project;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);

            vec2 left = texelFetch(u_velocity_texture, coord + ivec2(-1, 0), 0).xy;
            vec2 right = texelFetch(u_velocity_texture, coord + ivec2(1, 0), 0).xy;
            vec2 down = texelFetch(u_velocity_texture, coord + ivec2(0, -1), 0).xy;
            vec2 up = texelFetch(u_velocity_texture, coord + ivec2(0, 1), 0).xy;

            float div = -0.5 * u_grid_space * (right.x - left.x + up.y - down.y);

            o_project = vec2(0.0, div);
        }
    </script>
    <script type="x-shader/x-fragment" id="projection_step02_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_project_texture;

        out vec2 o_project;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);

            float div = texelFetch(u_project_texture, coord, 0).y;

            float left = texelFetch(u_project_texture, coord + ivec2(-1, 0), 0).x;
            float right = texelFetch(u_project_texture, coord + ivec2(1, 0), 0).x;
            float down = texelFetch(u_project_texture, coord + ivec2(0, -1), 0).x;
            float up = texelFetch(u_project_texture, coord + ivec2(0, 1), 0).x;

            o_project = vec2((div + left + right + down + up) / 4.0, div);
        }
    </script>
    <script type="x-shader/x-fragment" id="projection_step03_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_velocity_texture;
        uniform sampler2D u_project_texture;
        uniform float u_grid_space;
        uniform vec2 u_resolution;

        out vec2 o_velocity;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);
            ivec2 coordMax = ivec2(u_resolution);

            ivec2 vL = coord + ivec2(-1, 0);
            ivec2 vR = coord + ivec2(1, 0);
            ivec2 vD = coord + ivec2(0, -1);
            ivec2 vU = coord + ivec2(0, 1);

            vec2 velocity = texelFetch(u_velocity_texture, coord, 0).xy;

            float center = texelFetch(u_project_texture, coord, 0).x;

            float left = texelFetch(u_project_texture, vL, 0).x;
            float right = texelFetch(u_project_texture, vR, 0).x;
            float down = texelFetch(u_project_texture, vD, 0).x;
            float up = texelFetch(u_project_texture, vU, 0).x;

            o_velocity = velocity - 0.5 * vec2(right - left, up - down) / u_grid_space;
        }
    </script>
    <script type="x-shader/x-fragment" id="render_velocity_fs">#version 300 es
        precision highp float;

        #define PI 3.14159265359

        uniform sampler2D u_velocity_texture;

        float rand(float n) {return fract(sin(n) * 43758.5453123);}

        float noise(float p) {

            float fl = floor(p);
            float fc = fract(p);

            return mix(rand(fl), rand(fl + 1.0), fc);
        }

        out vec4 fragColor;

        void main(void) {
            ivec2 coord = ivec2(gl_FragCoord.xy);

            vec2 center = texelFetch(u_velocity_texture, coord, 0).xy;

            vec2 left = texelFetch(u_velocity_texture, coord + ivec2(-1, 0), 0).xy;
            vec2 right = texelFetch(u_velocity_texture, coord + ivec2(1, 0), 0).xy;
            vec2 down = texelFetch(u_velocity_texture, coord + ivec2(0, -1), 0).xy;
            vec2 up = texelFetch(u_velocity_texture, coord + ivec2(0, 1), 0).xy;

            vec2 velocity = center + left + right + down + up; 

            float brightness = min(1.0, pow(length(velocity), 1.5));

            vec3 color = vec3(noise(velocity.x) * brightness, noise(velocity.y) * brightness, noise(length(velocity)));

            fragColor = vec4(color * 1.5, 1.0);
        }
    </script>
    <script type="x-shader/x-fragment" id="render_density_fs">#version 300 es
        precision highp float;

        uniform sampler2D u_density_texture;

        out vec4 fragColor;

        void main(void) {
            vec3 density = texelFetch(u_density_texture, ivec2(gl_FragCoord.xy), 0).xyx;
            fragColor = vec4(density, 1.0);
        }
    </script>
</head>
<body>
    <canvas id="canvas"></canvas>
    <div id="container"></div>
    <h1>Stable Fluids</h1>
    <div id="parameter_container">
      <div class="parameter"><div class="parameter_name">Render<input type="checkbox" id="render"/><span id="disp_render" class="value"></span></div></div><br>
      <div class="parameter"><div class="parameter_name">Force Radius<span id="disp_force_rad" class="value"></span></div><input type="range" id="force_rad" min="0.005" max="0.05" value="0.01" step="0.001"/></div><br>
      <div class="parameter"><div class="parameter_name">Force Intensity<span id="disp_force_intensity" class="value"></span></div><input type="range" id="force_intensity" min="10" max="500" value="100" step="1"/></div><br>
      <div class="parameter"><div class="parameter_name">Diffuse<span id="disp_diffuse" class="value"></span></div><input type="range" id="diffuse" min="0.0" max="1.0" value="0.0" step="0.001"/></div><br>
      <div class="parameter"><div class="parameter_name">Time Step<span id="disp_time_step" class="value"></span></div><input type="range" id="time_step" min="0.001" max="0.01" value="0.005" step="0.0001"/></div><br>
    </div>
</body>
</html>

<style>
#container {
  padding-top: 50px;
  position: fixed;
}
canvas {
  position: absolute;
  top: 0;
  bottom: 0;
  right: 0;
  left: 0;
  margin: auto;
}
#parameter_container {
  background-color:rgba(20, 20, 20, 0.95);
  border-radius: 3%;
  padding: 2% 2.5%;
  width: calc(10% + 120px);
  font-family: 'Sen';
  font-size: calc(0.5vw + 11.35px);
  font-weight: 700;
  color: white;
  position: fixed;
  display: block;
  right: 50px;
  transition: all 500ms;
}
.parameter_name {
  width: 100%;
  padding-bottom: 10px;
}
.parameter {
  padding-top: 5px;
}
span {
  float: right;
}
input[type="range"] {
  -webkit-appearance: none;
  appearance: none;
  cursor: pointer;
  outline: none;
  background: rgba(255, 255, 255, 1.0);
  height: 3px;
  width: calc(100%);
}
input[type="range"]::-webkit-slider-thumb {
  -webkit-appearance:none;
  background:rgba(0.0, 0.0, 0.0, 0.0);
  height: 15px;
  width: 15px;
  border: 2px solid #494949;
  border-radius: 50%;
}
h1 {
  font-family: 'Sen';
  font-size: calc(1vw + 25px);
  font-weight: 700;
  left: 100px;
  bottom: 10px;
  position: fixed;
  color: white;
  transition: all 500ms;
}
</style>
